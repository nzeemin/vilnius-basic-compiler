	.TITLE	VIBAS

	.CSECT

CRLF:	.ASCIZ	<2><015><012>
	.EVEN

; Подпрограмма: Перевод строки
WRCRLF::
	MOV	#CRLF, R0

; Подпрограмма: Печать строки
; R0 = адрес строки, первый байт строки содержит длину
; Портит R1, R2
WRSTR::
	CLR	R2
	BISB	(R0)+, R2	; берём длину строки
	BEQ	9$		; пустая строка => выходим
1$:	MOVB	(R0)+, R1
2$:	TSTB	@#177564	; Источник канала 0 готов?
	BPL	2$		; нет => ждём
	MOV	R1, @#177566	; передаём символ в канал 0
	SOB	R2, 1$
9$:	RETURN

; Подпрограмма: Звуковой сигнал
BEEP::
	MOVB	#007, R0
	BR	WRCHR

; Подпрограмма: Очистка экрана
CLS::
	MOVB	#014, R0

WRCHR::
20$:	TSTB	@#177564	; Источник канала 0 готов?
	BPL	20$		; нет => ждём
	MOV	R0, @#177566	; передаём символ в канал 0
	RETURN

; Подпрограмма: PRINT AT(C,R), перемещение курсора
; R0 = колонка 0..255, R1 = строка 0..255
WRAT::
;TODO: Нормировать R0
;TODO: Нормировать R1
;TODO
	RETURN

; Подпрограмма: PRINT TAB(N), вывод пробелов пока не достигнем колонки N
WRTAB::
;TODO: Получить текущую колонку
;TODO
	RETURN

; Подпрограмма: PRINT SPC(N), печать пробелов, R0 = количество, R0 = 1..255
WRSPC::
	MOV	R0, R1
	MOV	#040, R0	; пробел
1$:	CALL	WRCHR
	SOB	R1, 1$
	RETURN

; Подпрограмма: Печать целого числа R0
;TODO: Печатать как -32768..32767
WRINT::
	MOV	R0, R4
	BPL	1$
	MOV	#'-, R0
	CALL	WRCHR
	NEG	R4
1$:	MOV	#060, R0
4$:	SUB	#023420, R4	; 10000.
	BMI	16$
	INC     R0
	BR      4$
16$:	ADD	#023420, R4
	CALL	WRCHR		; вывести символ R0 на экран
	MOV	#060, R0
30$:	SUB	#001750, R4	; 1000.
	BMI	42$
	INC     R0
	BR      30$
42$:	ADD	#001750, R4	; готова цифра тысяч
	CALL	WRCHR		; вывести символ R0 на экран
	MOV	#060, R0
54$:	SUB	#000144, R4	; 100.
	BMI	66$
	INC	R0
	BR	54$
66$:	ADD	#000144, R4	; Готова цифра сотен
	CALL	WRCHR		; вывести символ R0 на экран
	MOV	#060, R0
100$:	SUB	#000012, R4
	BMI	112$
	INC	R0
	BR	100$
112$:	ADD	#000012, R4	; готова цифра десятков
	CALL	WRCHR		; вывести символ R0 на экран
	MOV	#060, R0
	ADD	R4, R0		; готова последняя цифра
	CALL	WRCHR		; вывести символ R0 на экран
	RETURN

; Подпрограмма: Копирование строки из R0 в R1
STRCPY::
	CLR	R2
	BISB	(R0)+, R2
	MOVB	R2, (R1)+
	BEQ	9$
1$:	MOVB	(R0)+, (R1)+
	SOB	R2, 1$
9$:	RETURN

    .END
